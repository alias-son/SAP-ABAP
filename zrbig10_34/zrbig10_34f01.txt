*&---------------------------------------------------------------------*
*&  Include           ZRBIG10_34F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  SET_INIT_VALUE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM set_init_value .

  p_rbukrs  = '1000'.
  p_ryear   = sy-datum(4).
  p_rldnr   = '0L'.
  p_rvers   = '1'.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  GET_SUMMARY_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM get_summary_data .
**********************************************************************
* Get FAGLFLEXT data
**********************************************************************
  CLEAR : gt_flext, gs_flext.
  SELECT racct  " G/L Account
         drcrk  " D/C Indicator
         SUM( hslvt ) AS hslvt
    INTO CORRESPONDING FIELDS OF TABLE gt_flext
    FROM faglflext
    WHERE rbukrs  EQ p_rbukrs
    AND   ryear   EQ p_ryear
    AND   rldnr   EQ p_rldnr
    AND   rvers   EQ p_rvers
    GROUP BY racct drcrk.

  IF sy-subrc NE 0.
    MESSAGE s037(k5) DISPLAY LIKE 'E'.
    STOP.
  ENDIF.
ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  GET_TEXT_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM get_text_data .

  DATA : lt_flext TYPE TABLE OF faglflext WITH HEADER LINE.

  CLEAR : gt_skat, gs_skat, lt_flext[], lt_flext.

*-- Data backup <- ## #### ### #### ## BACKUP TABLE ##
  lt_flext[] = gt_flext.

*-- Delete duplicate data
  SORT lt_flext BY racct ASCENDING.
  DELETE ADJACENT DUPLICATES FROM lt_flext COMPARING racct.

*-- Get text data
  SELECT *
    INTO CORRESPONDING FIELDS OF TABLE gt_skat
    FROM skat
    FOR ALL ENTRIES IN lt_flext
    WHERE spras EQ sy-langu
    AND   saknr EQ lt_flext-racct
    AND   ktopl EQ 'CAIN'.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  MAKE_DISPLAY_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM make_display_data .

  SORT gt_skat BY saknr ASCENDING.

  CLEAR : gt_body, gs_body.
  LOOP AT gt_flext INTO gs_flext.

*-- Set G/L Account
    gs_body-racct = gs_flext-racct.

*-- Set amount separate by D/C indicator
    CASE gs_flext-drcrk.
      WHEN 'S'. " Debit (##)
        gs_body-debit = gs_flext-hslvt.
      WHEN 'H'. " Credit (##)
        gs_body-credit = gs_flext-hslvt.
    ENDCASE.

*-- Set G/L account name
    CLEAR gs_skat.
    READ TABLE gt_skat INTO gs_skat WITH KEY saknr = gs_body-racct
                                    BINARY SEARCH.

    IF sy-subrc EQ 0.
      gs_body-txt50 = gs_skat-txt50.
    ENDIF.

*    APPEND gs_body TO gt_body.
    COLLECT gs_body INTO gt_body.
    CLEAR gs_body.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  DISPLAY_SCREEN
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM display_screen .
  PERFORM set_layout.

  CLEAR : gt_fcat, gs_fcat.

  PERFORM set_field_catalog USING :
        "PK FIELD     COLTXT          EMPHSIZE
        ' ' 'DEBIT'   'Debit'         ' ',
        ' ' 'RACCT'   'G/L Account'   ' ',
        ' ' 'TXT50'   'G/L Text'      'X',
        ' ' 'CREDIT'  'Credit'        ' '.

  IF go_container IS INITIAL.

    PERFORM create_object.

    SET HANDLER : lcl_event_handler=>on_hotspot_click FOR go_alv_grid.

    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        i_save                        = 'A'
        i_default                     = 'X'
        is_layout                     = gs_layout
      CHANGING
        it_outtab                     = gt_body
        it_fieldcatalog               = gt_fcat
*       it_sort                       =
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        OTHERS                        = 4.

  ELSE.
    CALL METHOD go_alv_grid->refresh_table_display.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SET_LAYOUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM set_layout .
  gs_layout-zebra       = 'X'.
  gs_layout-sel_mode    = 'D'.
  gs_layout-cwidth_opt  = 'X'.
ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SET_FIELD_CATALOG
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_0205   text
*      -->P_0206   text
*      -->P_0207   text
*      -->P_0208   text
*----------------------------------------------------------------------*
FORM set_field_catalog  USING pv_key pv_field pv_text pv_emphasize.

  gs_fcat-key       = pv_key.
  gs_fcat-fieldname = pv_field.
  gs_fcat-coltext   = pv_text.
  gs_fcat-emphasize = pv_emphasize.

  CASE pv_field.
    WHEN 'RACCT'.
      gs_fcat-hotspot = 'X'.
  ENDCASE.

  APPEND gs_fcat TO gt_fcat.
  CLEAR  gs_fcat.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  CREATE_OBJECT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM create_object .
  CREATE OBJECT go_container
    EXPORTING
      repid     = sy-repid
      dynnr     = sy-dynnr
      side      = go_container->dock_at_left
      extension = 3000.
  CREATE OBJECT go_alv_grid
    EXPORTING
      i_parent = go_container.
ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  HOTSPOT_CLICK
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_E_ROW_ID  text
*----------------------------------------------------------------------*
FORM hotspot_click  USING pv_row_id.

  CLEAR : gs_body.
  READ TABLE gt_body INTO gs_body INDEX pv_row_id.

  SET PARAMETER ID 'ACC' FIELD gs_body-racct.
  SET PARAMETER ID 'BUK' FIELD p_rbukrs.
  SET PARAMETER ID 'GJR' FIELD p_ryear.
  SET PARAMETER ID 'GLN_FLEX' FIELD p_rldnr.

  CALL TRANSACTION 'FAGLB03' AND SKIP FIRST SCREEN.

ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 750
